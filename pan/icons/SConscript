import os,sys
from subprocess import *

Import('env')

files=Glob('*.png')

def mkpbint(target,source,env):
    out=open(str(target[0]),'wb')
    out.write("""
#ifndef PAN_PIXBUF_INTERNAL_H
#define PAN_PIXBUF_INTERNAL_H

""")
    for x in source:
        f=str(x)
        n=os.path.splitext(os.path.basename(f))[0]
        p=Popen('gdk-pixbuf-csource --extern --raw --build-list %s %s'%(n,f),
            stdout=out,stderr=sys.stderr)
        p.wait()
    out.write("\n#endif\n")
    out.close()

def mkpanpix(target,source,env):
    fin=open(str(source[0]),'rb')
    out=open(str(target[0]),'wb')
    out.write("""
#ifndef PAN_PIXBUF_H
#define PAN_PIXBUF_H

""")
    for line in fin:
        if line.startswith('const guint8 icon_') and line.rfind('__attribute')==-1 and line.find('pragma')==-1 :
            e=line.rfind('=')-1
            out.write('extern %s;\n'%(line[0:e]))
    fin.close()
    out.write("\n#endif\n")
    out.close()
    
ppi=env.Command('pan-pixbufs-internal.h',files,mkpbint)
pp=env.Command('pan-pixbufs.h',ppi,mkpanpix)

env.Alias('icons',pp)
